<!doctype HTML>
<html lang="en">
<meta charset="utf-8">
<head>
<link href="/css/2019-11-01.css" rel="stylesheet">
<title>Source &amp; Binary / Adding non-Administrator roles for humans</title>
</head>
<body>
<header><h1><a href="/">Source <span class="fancy">&amp;</span> Binary</a></h1></header>

<h1>Adding non-Administrator roles for humans</h1>

<p>In order to set the <code>RoleName</code> attribute on any of your IdP users to any value except &ldquo;Administrator&rdquo;, you&rsquo;ll need to define a role in your admin account and possibly some (or all) of your service accounts.</p>

<h2>In your admin account(s)</h2>

<p>In a new file in <code>modules/intranet/global</code>, add code like this:</p>

<pre><code>data "aws_iam_policy_document" "developer" {
  statement {
    actions = [
      "ec2:DescribeVpcs", # an example for demonstration purposes; could be anything, including wildcards
      "sts:AssumeRole",   # required to use /accounts and the AWS Console
    ]
    resources = ["*"]
  }
}

data "aws_iam_policy_document" "developer-trust" {
  statement {
    actions = ["sts:AssumeRole"]
    principals {
      identifiers = [ # both are references to stanzas in main.tf
        data.aws_iam_user.credential-factory.arn,
        module.intranet.role_arn,
      ]
      type = "AWS"
    }
    principals {
      identifiers = ["ec2.amazonaws.com"] # for Instance Factory
      type        = "Service"
    }
  }
}

resource "aws_iam_instance_profile" "developer" {
  name = "Developer"
  role = aws_iam_role.developer.name
}

resource "aws_iam_policy" "developer" {
  name   = "Developer"
  policy = data.aws_iam_policy_document.developer.json
}

resource "aws_iam_role" "developer" {
  assume_role_policy   = data.aws_iam_policy_document.developer-trust.json
  max_session_duration = 43200
  name                 = "Developer"
}

resource "aws_iam_role_policy_attachment" "developer" {
  policy_arn = aws_iam_policy.developer.arn
  role       = aws_iam_role.developer.name
}
</code></pre>

<p>This role is called &ldquo;Developer&rdquo; but of course you can call yours whatever you like and have as many as you like.</p>

<p>Run <code>substrate-create-admin-account -quality="..."</code> to create this role, then update the <code>RoleName</code> attribute in your IdP to assign it to whichever users you like.</p>

<h1>In your service accounts</h1>

<p>In order for users assigned this role to assume roles in other AWS accounts, your service accounts, you&apos;ll need to create roles in those accounts, too, and authorize this role to assume them. You do this on each account individually.</p>

<p>In each service account, code like this in a new file in <code>modules/<em>domain</em>/global</code> or <code>root-modules/<em>domain</em>/<em>environment</em>/<em>quality</em>/global</code> will allow these new non-Administrator users to assume a role in that account:</p>

<pre><code>data "aws_iam_policy_document" "developer" {
  statement {
    actions = [
      "ec2:DescribeVpcs", # an example for demonstration purposes; could be anything, including wildcards
    ]
    resources = ["*"]
  }
}

data "aws_iam_policy_document" "developer-trust" {
  statement {
    actions = ["sts:AssumeRole"]
    principals {
      identifiers = [
        # admin account number or
        # ARN of the role you created above
      ]
      type = "AWS"
    }
  }
}

resource "aws_iam_policy" "developer" {
  name   = "Developer"
  policy = data.aws_iam_policy_document.developer.json
}

resource "aws_iam_role" "developer" {
  assume_role_policy   = data.aws_iam_policy_document.developer-trust.json
  max_session_duration = 43200
  name                 = "Developer"
}

resource "aws_iam_role_policy_attachment" "developer" {
  policy_arn = aws_iam_policy.developer.arn
  role       = aws_iam_role.developer.name
}
</code></pre>

<p>Run <code>substrate-create-account -domain="..." -environment="..." -quality="..."</code> to create this role.</p>

<footer>
<ul>
<li><a href="/services/">Services</a>: <small>infrastructure, architecture, compliance</small></li>
<li><a href="/substrate/">Substrate</a>, <small>my suite of AWS management tools</small></li>
</ul>
<p>Copyright &copy; 2020-2021 &mdash; Source <span class="fancy">&amp;</span> Binary LLC</p>
</footer>
</body>
</html>
